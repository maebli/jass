plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id 'java'
}

group 'ch.games'
version '0.0.3'


repositories {
    mavenCentral()
}

tasks.named('jar') {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version
                )
    }
}

java {
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/maebli/jass"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'jass-library'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Jass Library'
                description = 'A library to use to play the game Jass.'
                url = 'https://github.com/maebli/jass'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License v2.1'
                        url = 'https://github.com/maebli/jass/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        name = 'maebli'
                    }
                }
            }
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.13'
}



